apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'org.greenrobot.greendao'

android {
    def appName = "iFAFU"
    //noinspection GradleCompatible
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId "cn.ifafu.ifafu"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 34
        versionName "1.0.13"
        resConfigs "zh"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            //为debug版本的包名添加.debug后缀
            applicationIdSuffix ".debug"
            manifestPlaceholders = [
                    app_name: "@string/app_name_test",
                    app_icon: "@mipmap/ic_launcher_debug",
                    app_icon_round: "@mipmap/ic_launcher_round_debug",
                    app_icon_hd: "@drawable/icon_ifafu_round_test"
            ]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    app_name: "@string/app_name",
                    app_icon: "@mipmap/ic_launcher",
                    app_icon_round: "@mipmap/ic_launcher_round",
                    app_icon_hd: "@drawable/icon_ifafu_round"
            ]
        }
    }

    flavorDimensions "default"
    productFlavors {

        web {

        }

        coolapk {

        }

        huawei {
            
        }

        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [channel: name]
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name != "debug") {
            variant.getPackageApplication().outputDirectory = new File("${project.rootDir.absolutePath}/app/apk/${variant.name}")
        }
        variant.outputs.all {
            outputFileName = "${appName}${defaultConfig.versionName}_${defaultConfig.versionCode}.apk"
        }
    }


    greendao {
        //版本号，升级时可配置
        schemaVersion 31
        daoPackage 'cn.ifafu.ifafu.dao'
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0-beta04"
    implementation 'androidx.core:core:1.2.0-alpha04'
    implementation 'com.google.android.material:material:1.1.0-alpha10'
    //Kotlin
    implementation "androidx.core:core-ktx:1.1.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //选择器
    implementation 'com.contrarywind:Android-PickerView:4.1.7'
    implementation 'com.orhanobut:dialogplus:1.11@aar'
    implementation 'com.zhihu.android:matisse:0.5.3-beta2'
//    implementation 'com.nfleo:MatisseKotlin:1.1.1'
//    implementation 'com.jph.takephoto:takephoto_library:4.+'
    //刷新库
    implementation 'com.github.ybq:Android-SpinKit:1.2.0'
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0'
    //Bugly
    implementation 'com.tencent.bugly:crashreport_upgrade:1.4.2'
    //沉浸状态栏
    implementation 'com.jaeger.statusbarutil:library:1.5.1'
    implementation 'com.gyf.immersionbar:immersionbar:3.0.0-beta05'
    //RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.12'
    implementation 'com.github.ReactiveX:RxKotlin:2.4.0'
    //Http框架
    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.1'
    implementation 'org.jsoup:jsoup:1.12.1'
    implementation 'com.alibaba:fastjson:1.2.60'
    //数据库框架
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'io.github.yuweiguocn:GreenDaoUpgradeHelper:v2.2.1'
    //Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    //动画低版本支持
    implementation 'com.nineoldandroids:library:2.4.0'
    //ViewAdapter
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.50'
    implementation 'me.drakeet.multitype:multitype:4.0.0-alpha3'
    //权限管理
    implementation "org.permissionsdispatcher:permissionsdispatcher:4.5.0"
    annotationProcessor "org.permissionsdispatcher:permissionsdispatcher-processor:4.5.0"
    //Material Dialogs: https://github.com/afollestad/material-dialogs
    implementation 'com.afollestad.material-dialogs:core:3.1.1'
    implementation 'com.afollestad.material-dialogs:color:3.1.1'
    implementation 'com.afollestad.material-dialogs:datetime:3.1.0'
    implementation 'com.afollestad.material-dialogs:bottomsheets:3.1.1'
    //本地异常捕捉框架: https://github.com/Ereza/CustomActivityOnCrash
    implementation 'cat.ereza:customactivityoncrash:2.2.0'
    //DialogPlus: https://github.com/orhanobut/dialogplus
    implementation 'com.orhanobut:dialogplus:1.11@aar'
    //CircleImageView: https://github.com/hdodenhof/CircleImageView
    implementation 'de.hdodenhof:circleimageview:3.0.1'
    //U-App移动统计: https://developer.umeng.com/docs/119267/detail/118584
    implementation 'com.umeng.umsdk:analytics:8.0.0'
    implementation 'com.umeng.umsdk:common:2.0.0'

    testImplementation 'junit:junit:4.13-beta-3'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
}

repositories {
    mavenCentral()
}